INCLUDE ~item_rev/lib/compatibility.tpa~
INCLUDE ~item_rev/lib/descriptions.tpa~
INCLUDE ~item_rev/lib/usability_description.tpa~

//////////////////////////////////////////////
//  kit description

COPY_EXISTING kitlist.2da override
  READ_2DA_ENTRY 3 4 9 desc
  SET_2DA_ENTRY 3 4 9 ~kensai_bracer~
  GET_STRREF desc kensai
  INNER_PATCH_SAVE bracer ~%kensai%~ BEGIN
    SPRINT t1 @100413
    SPRINT t2 @100414
    REPLACE_TEXTUALLY ~%t1%~ ~%t2%~
  END
  REPLACE ~kensai_bracer~ ~%bracer%~

//////////////////////////////////////////////
//  descriptions

DEFINE_PATCH_MACRO kensai_bracers BEGIN
  SPRINT add_class @100415
  SPRINT catch_class @100416
  LPF usable_desc INT_VAR make_usable=1 add_where=0 STR_VAR add_class catch_class END
END

//////////////////////////////////////////////
//  patching

/*
ACTION_FOR_EACH bracer IN
  brac01 // Bracers of Defense AC 8
  brac02 // Bracers of Defense AC 7
  brac03 // Bracers of Defense AC 6
  brac05 // Bracers
  brac11 // Bracers of Binding
  brac12 // Bracers
  brac13 // Bracers of Defense AC 5
  brac14 // Bracers of Defense AC 4
  brac15 // Bracers of Defense AC 3
  brac16 // Bracers of Blinding Strike
  brac26 // Tzu-Zan's Bracers
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%bracer%.itm~ BEGIN
    COPY_EXISTING ~%bracer%.itm~ override
      PATCH_IF SOURCE_SIZE>0x71 BEGIN
        WRITE_BYTE 0x2f (THIS & `4)
      END
  END
END
*/

// use Mike's version, it's much better
COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
    READ_BYTE  0x18 flags1
    READ_SHORT 0x1c type
    PATCH_IF (type == 0x06) BEGIN // bracers/gauntlets
      PATCH_IF ((flags1 BAND 0b00000100) == 0b00000100) BEGIN // players can use
        TO_UPPER ~SOURCE_RES~
        READ_BYTE 0x2f kit_usability4
        // if unusable by kensais
        PATCH_IF ((kit_usability4 BAND 0b00000100) == 0b00000100) BEGIN
          // check that the item is not a pair of gauntlets
          PATCH_IF (
              (~%SOURCE_RES%~ STR_CMP ~_BRAC04~) && // Bracers of Archery [kensais can't use missile weapons either]
              (~%SOURCE_RES%~ STR_CMP ~_BRAC06~) && // Gauntlets of Ogre Power
              (~%SOURCE_RES%~ STR_CMP ~_BRAC07~) && // Gauntlets of Dexterity
              (~%SOURCE_RES%~ STR_CMP ~_BRAC08~) && // Gauntlets of Fumbling
              (~%SOURCE_RES%~ STR_CMP ~_BRAC09~) && // Gauntlets of Parrying
              (~%SOURCE_RES%~ STR_CMP ~_BRAC10~) && // Gauntlets of Power Attacks
              (~%SOURCE_RES%~ STR_CMP ~ADGAUNT~) && // Sembian Spellbreaks
              (~%SOURCE_RES%~ STR_CMP ~AGGUAN01~) && // Gauntlets of Time Stopping
              (~%SOURCE_RES%~ STR_CMP ~BRAC04~) && // Bracers of Archery [kensais can't use missile weapons either]
              (~%SOURCE_RES%~ STR_CMP ~BRAC06~) && // Gauntlets of Ogre Power
              (~%SOURCE_RES%~ STR_CMP ~BRAC07~) && // Gauntlets of Dexterity
              (~%SOURCE_RES%~ STR_CMP ~BRAC08~) && // Gauntlets of Fumbling
              (~%SOURCE_RES%~ STR_CMP ~BRAC09~) && // Gauntlets of Parrying
              (~%SOURCE_RES%~ STR_CMP ~BRAC10~) && // Gauntlets of Power Attacks
           (comp_cursed=0 || ((~%SOURCE_RES%~ STR_CMP ~BRAC11~) && comp_cursed) ) && // Gauntlets of the Drow [only when Cursed Items is installed]
              (~%SOURCE_RES%~ STR_CMP ~BRAC19~) && // Gauntlets of Crushing
              (~%SOURCE_RES%~ STR_CMP ~BRAC20~) && // Gloves of Healing
              (~%SOURCE_RES%~ STR_CMP ~BRAC21~) && // Gauntlets of Extraordinary Specialization
              (~%SOURCE_RES%~ STR_CMP ~BRAC22~) && // Paladin's Bracers
              (~%SOURCE_RES%~ STR_CMP ~BRAC23~) && // Blessed Bracers
              (~%SOURCE_RES%~ STR_CMP ~BRAC24~) && // Bard's Gloves
              (~%SOURCE_RES%~ STR_CMP ~BRAC25~) && // Wondrous Gloves
              (~%SOURCE_RES%~ STR_CMP ~BRACGR01~) && // Gauntlets of the Faithful Warrior
              (~%SOURCE_RES%~ STR_CMP ~BRC01~) && // Gauntlets of Might
              (~%SOURCE_RES%~ STR_CMP ~CBBRCSPL~) && // Gauntlets of Spell Casting
              (~%SOURCE_RES%~ STR_CMP ~CBCL023A~) && // Gauntlets of Valor
              (~%SOURCE_RES%~ STR_CMP ~CMTUBR01~) && // Mage Gauntlets
              (~%SOURCE_RES%~ STR_CMP ~COMG01~) && // Gauntlets of Valor
              (~%SOURCE_RES%~ STR_CMP ~DEITM092~) && // Baelden's Gauntlets
              (~%SOURCE_RES%~ STR_CMP ~DEITM103~) && // Gauntlets of Shau'Usi
              (~%SOURCE_RES%~ STR_CMP ~DEITM104~) && // Gauntlets of Verova
              (~%SOURCE_RES%~ STR_CMP ~DSBRAC04~) && // Gauntlets of the Faithful Warrior
              (~%SOURCE_RES%~ STR_CMP ~DSBRACDX~) && // Gauntlets of Dexterity
              (~%SOURCE_RES%~ STR_CMP ~FWBRAC04~) && // Sunus Graywar's Gauntlets
              (~%SOURCE_RES%~ STR_CMP ~FWBRAC07~) && // Gauntlets of Shocking
              (~%SOURCE_RES%~ STR_CMP ~NEWBRLI~) && // Gauntlets of Life
              (~%SOURCE_RES%~ STR_CMP ~O!BOM025~) && // Gauntlets of the Silver Knight
              (~%SOURCE_RES%~ STR_CMP ~O#DCBRA2~) && // Gauntlets of Heavy Strike
              (~%SOURCE_RES%~ STR_CMP ~R#ASMGAU~) && // Minor Mage Gauntlets
              (~%SOURCE_RES%~ STR_CMP ~S#BRAC01~) && // Gauntlets of Grandmastery
              (~%SOURCE_RES%~ STR_CMP ~S#BRAC02~) && // Gauntlets of Crushing 'Hand of Vampire'
              (~%SOURCE_RES%~ STR_CMP ~SGBRAC02~) && // Bloodlust gauntlets
              (~%SOURCE_RES%~ STR_CMP ~THBRAC01~) && // Gauntlets of Grandmastery
              (~%SOURCE_RES%~ STR_CMP ~U#BRAC03~) && // Gauntlets of Assassination
              (~%SOURCE_RES%~ STR_CMP ~ULFISTS~) && // Fighting Gauntlets
              (~%SOURCE_RES%~ STR_CMP ~WZRDBRAC~) // Gauntlets of the Magi
          ) BEGIN
            // make usable by kensais
            WRITE_BYTE 0x2f (kit_usability4 BAND 0b11111011)
            // update description
            SPRINT text_update ~kensai_bracers~
            LAUNCH_PATCH_MACRO ~update_item_descriptions~
          END
        END
      END
    END
  END
  BUT_ONLY





