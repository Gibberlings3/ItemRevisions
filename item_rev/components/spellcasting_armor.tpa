INCLUDE ~item_rev/lib/descriptions.tpa~
INCLUDE ~item_rev/lib/macros.tpa~
INCLUDE ~item_rev/lib/armor_list.tpa~
INCLUDE ~item_rev/lib/common_shield_list.tpa~
INCLUDE ~item_rev/lib/multiclass_standard.tpa~

//////////////////////////////////////////////
//  descriptions

DEFINE_PATCH_MACRO ~remove_old_spellcasting_lines~ BEGIN
  SPRINT t1 @100300
  REPLACE_TEXTUALLY ~%t1%~ ~~
  SPRINT t1 @100301
  REPLACE_TEXTUALLY ~%t1%~ ~~
  SPRINT t1 @100302
  REPLACE_TEXTUALLY ~%t1%~ ~~
  SPRINT t1 @100303
  REPLACE_TEXTUALLY ~%t1%~ ~~
  SPRINT t1 @100304
  REPLACE_TEXTUALLY ~%t1%~ ~~
END

DEFINE_PATCH_MACRO ~casting_penalty~ BEGIN
  PATCH_IF xfail>0 BEGIN
    SPRINT t1 @100114
    SPRINT t2 @100305
    REPLACE_TEXTUALLY ~%t1%~ ~%t2%~
  END
  PATCH_IF xspd>0 && option=2 BEGIN
    SPRINT t1 @100114
    SPRINT t2 @100306
    REPLACE_TEXTUALLY ~%t1%~ ~%t2%~
  END
  PATCH_IF xspd>0 && option=3 BEGIN
    SPRINT t1 @100114
    SPRINT t2 @100307
    REPLACE_TEXTUALLY ~%t1%~ ~%t2%~
  END
END


//////////////////////////////////////////////
//  reading table and other precautions


DEFINE_PATCH_MACRO calc BEGIN
  READ_2DA_ENTRY_FORMER rn armor column value
  PATCH_IF subst=2 BEGIN value+= EVAL ~rn_8_%column%~ END // scale
  PATCH_IF subst=1 BEGIN value+= EVAL ~rn_9_%column%~ END // mithral
  PATCH_IF elven BEGIN value+= EVAL ~rn_10_%column%~ END
  value= value>0 ? value : 0
END


DEFINE_PATCH_MACRO cleanup BEGIN
  LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete=60 END
  LPF DELETE_ITEM_EQEFFECT INT_VAR opcode_to_delete=145 END
  SPRINT text_update ~remove_old_spellcasting_lines~
  LPM ~update_item_descriptions~
END


COPY - ~item_rev/lib/armor_penalty_table.txt~ override
  READ_2DA_ENTRIES_NOW rn 4


// make new casting speed penalty EFFs
ACTION_IF option=2 || option=3 BEGIN
  // shields
  OUTER_FOR (i=11;i<15;i+=1) BEGIN
    OUTER_SET val= EVAL ~rn_%i%_3~
    ACTION_IF val>0 BEGIN
      OUTER_SET $spd("%val%")=1
    END
  END
  // armors
  OUTER_FOR (i=1;i<8;i+=1) BEGIN
    OUTER_SET val= EVAL ~rn_%i%_3~
    ACTION_IF val>0 BEGIN
      OUTER_SET $spd("%val%")=1
    END
    // special armor materials
    OUTER_FOR (j=8;j<10;j+=1) BEGIN
      OUTER_SET val= EVAL ~rn_%i%_3~ + EVAL ~rn_%j%_3~
      OUTER_SET val= val>0 ? val : 0
      ACTION_IF val>0 BEGIN
        OUTER_SET $spd("%val%")=1
      END
      // elven craftsmanship
      OUTER_SET val += EVAL ~rn_10_3~
      OUTER_SET val= val>0 ? val : 0
      ACTION_IF val>0 BEGIN
        OUTER_SET $spd("%val%")=1
      END
    END
  END
  ACTION_PHP_EACH spd AS i => r BEGIN
    LAF CREATE_EFFECT INT_VAR opcode=189 target=1 parameter1=(0 - i) timing=2 STR_VAR effect= EVAL ~ag#irs%i%~ END
  END
END


//////////////////////////////////////////////
//  patching armors

ACTION_PHP_EACH armor AS ind => res BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%ind%.itm~ BEGIN
    COPY_EXISTING ~%ind%.itm~ override PATCH_IF SOURCE_SIZE>0x71 BEGIN
      LPM cleanup

      spell = ( res >> 7 ) & 1 // can cast without penalties in this armor
      armor =   res & 7
      PATCH_IF spell=0 && armor BEGIN
        xspd=0
        xfail=0

        subst = ( res >> 3 ) & 3 // 1=mithral, 2=scale
        elven = ( res >> 5 ) & 1

        PATCH_IF option=1 BEGIN
          column=2 LPM calc
          PATCH_IF value BEGIN
            xfail=value
            LPF ADD_ITEM_EQEFFECT INT_VAR opcode=60 target=1 timing=2 parameter1=value END
          END
        END

        PATCH_IF option=2 || option=3 BEGIN
          column=3 LPM calc
          PATCH_IF value BEGIN
            xspd=value
            PATCH_FOR_EACH class IN
              1  // mage
              5  // bard
              7  // fighter/mage
              10 // fighter/mage/thief
              13 // mage/thief
              14 // cleric/mage
              17 // fighter/mage/cleric
            BEGIN
              LPF ADD_ITEM_EQEFFECT INT_VAR opcode=177 target=1 timing=2 parameter1=class parameter2=5 STR_VAR resource=EVAL ~ag#irs%value%~ END
            END
            PATCH_IF option=3 BEGIN
              PATCH_FOR_EACH class IN
                3  // cleric
                6  // paladin
                8  // fighter/cleric
                11 // druid
                12 // ranger
                15 // cleric/thief
                16 // fighter/druid
                18 // cleric/ranger
              BEGIN
                LPF ADD_ITEM_EQEFFECT INT_VAR opcode=177 target=1 timing=2 parameter1=class parameter2=5 STR_VAR resource=EVAL ~ag#irs%value%~ END
              END
            END
          END
        END

        SPRINT text_update casting_penalty
        LPM update_item_descriptions

      END

    END BUT_ONLY
  END
END

//////////////////////////////////////////////
//  patching shields

LAM shields

COPY_EXISTING_REGEXP ~^.+\.itm$~ override PATCH_IF SOURCE_SIZE>0x71 BEGIN
  READ_SHORT 0x1c item
  READ_ASCII 0x22 anim (2)

  type=0
  PATCH_IF item=190 || (item=0xc && (~%anim%~ STRING_EQUAL_CASE ~d1~) ) BEGIN // buckler
    LPM cleanup
    type=11
    WRITE_SHORT 0x1c 0xc
  END
  PATCH_IF item=191 || (item=0xc && (~%anim%~ STRING_EQUAL_CASE ~d2~) ) BEGIN // light
    LPM cleanup
    type=12
    WRITE_SHORT 0x1c 0xc
  END
  PATCH_IF item=192 || (item=0xc && (~%anim%~ STRING_EQUAL_CASE ~d3~) ) BEGIN // heavy
    LPM cleanup
    type=13
    WRITE_SHORT 0x1c 0xc
  END
  PATCH_IF item=194 || (item=0xc && (~%anim%~ STRING_EQUAL_CASE ~d4~) ) BEGIN // tower
    LPM cleanup
    type=14
    WRITE_SHORT 0x1c 0xc
  END

  PATCH_IF option=1 && type BEGIN
    xfail=0
    READ_2DA_ENTRY_FORMER rn type 2 xfail
    PATCH_IF xfail BEGIN
      LPF ADD_ITEM_EQEFFECT INT_VAR opcode=60 target=1 timing=2 parameter1=xfail END
      SPRINT text_update casting_penalty
      LPM update_item_descriptions
    END
  END
  PATCH_IF (option=2 || option=3) && type BEGIN
    xspd=0
    READ_2DA_ENTRY_FORMER rn type 3 xspd
    PATCH_IF xspd BEGIN
      PATCH_FOR_EACH class IN
        1  // mage
        5  // bard
        7  // fighter/mage
        10 // fighter/mage/thief
        13 // mage/thief
        14 // cleric/mage
        17 // fighter/mage/cleric
      BEGIN
        LPF ADD_ITEM_EQEFFECT INT_VAR opcode=177 target=1 timing=2 parameter1=class parameter2=5 STR_VAR resource=EVAL ~ag#irs%xspd%~ END
      END
      PATCH_IF option=3 BEGIN
        PATCH_FOR_EACH class IN
          3  // cleric
          6  // paladin
          8  // fighter/cleric
          11 // druid
          12 // ranger
          15 // cleric/thief
          16 // fighter/druid
          18 // cleric/ranger
        BEGIN
          LPF ADD_ITEM_EQEFFECT INT_VAR opcode=177 target=1 timing=2 parameter1=class parameter2=5 STR_VAR resource=EVAL ~ag#irs%xspd%~ END
        END
      END
      SPRINT text_update casting_penalty
      LPM update_item_descriptions
    END
  END

END BUT_ONLY


//////////////////////////////////////////////
//  class descriptions

// update strings displayed at character creation to reflect these changes
OUTER_PATCH ~~ BEGIN

  GET_STRREF 9573 fc
  INNER_PATCH_SAVE fc ~%fc%~ BEGIN
    PATCH_IF option=3 BEGIN
      REPLACE_TEXTUALLY ~%van_spl%~ ~%new_spl%~
      REPLACE_TEXTUALLY ~%gtu_spl%~ ~%new_spl%~
    END
  END

  GET_STRREF 9574 fm
  INNER_PATCH_SAVE fm ~%fm%~ BEGIN
    PATCH_IF option=1 || option=2 || option=3 BEGIN
      REPLACE_TEXTUALLY ~%van_spl%~ ~%new_spl%~
      REPLACE_TEXTUALLY ~%gtu_spl%~ ~%new_spl%~
    END
    PATCH_IF option=4 BEGIN
      REPLACE_TEXTUALLY ~%van_spl%~ ~~
      REPLACE_TEXTUALLY ~%gtu_spl%~ ~~
    END
  END

  GET_STRREF 9575 mt
  INNER_PATCH_SAVE mt ~%mt%~ BEGIN
    PATCH_IF option=1 || option=2 || option=3 BEGIN
      REPLACE_TEXTUALLY ~%van_spl%~ ~%new_spl%~
      REPLACE_TEXTUALLY ~%gtu_spl%~ ~%new_spl%~
    END
    PATCH_IF option=4 BEGIN
      REPLACE_TEXTUALLY ~%van_spl%~ ~~
      REPLACE_TEXTUALLY ~%gtu_spl%~ ~~
    END
  END

  GET_STRREF 9576 fmt
  INNER_PATCH_SAVE fmt ~%fmt%~ BEGIN
    PATCH_IF option=1 || option=2 || option=3 BEGIN
      REPLACE_TEXTUALLY ~%van_spl%~ ~%new_spl%~
      REPLACE_TEXTUALLY ~%gtu_spl%~ ~%new_spl%~
    END
    PATCH_IF option=4 BEGIN
      REPLACE_TEXTUALLY ~%van_spl%~ ~~
      REPLACE_TEXTUALLY ~%gtu_spl%~ ~~
    END
  END

  GET_STRREF 9577 mc
  INNER_PATCH_SAVE mc ~%mc%~ BEGIN
    PATCH_IF option=1 BEGIN
      REPLACE_TEXTUALLY ~%van_wsp%~ ~%new_wsp%~
      REPLACE_TEXTUALLY ~%gtu_wsp%~ ~%new_wsp%~
    END
    PATCH_IF option=2 || option=3 BEGIN
      REPLACE_TEXTUALLY ~%van_wsp%~ ~%new_spl%~
      REPLACE_TEXTUALLY ~%gtu_wsp%~ ~%new_spl%~
    END
    PATCH_IF option=4 BEGIN
      REPLACE_TEXTUALLY ~%van_wsp%~ ~~
      REPLACE_TEXTUALLY ~%gtu_wsp%~ ~~
    END
  END

  GET_STRREF 9578 tc
  INNER_PATCH_SAVE tc ~%tc%~ BEGIN
    PATCH_IF option=3 BEGIN
      REPLACE_TEXTUALLY ~%van_spl%~ ~%new_spl%~
      REPLACE_TEXTUALLY ~%gtu_spl%~ ~%new_spl%~
    END
  END

  GET_STRREF 9579 fd
  INNER_PATCH_SAVE fd ~%fd%~ BEGIN
    PATCH_IF option=3 BEGIN
      REPLACE_TEXTUALLY ~%van_spl%~ ~%new_spl%~
      REPLACE_TEXTUALLY ~%gtu_spl%~ ~%new_spl%~
    END
  END

  GET_STRREF 9580 cr
  INNER_PATCH_SAVE cr ~%cr%~ BEGIN
    PATCH_IF option=3 BEGIN
      REPLACE_TEXTUALLY ~%van_spl%~ ~%new_spl%~
      REPLACE_TEXTUALLY ~%gtu_spl%~ ~%new_spl%~
    END
  END

  GET_STRREF 9581 fmc
  INNER_PATCH_SAVE fmc ~%fmc%~ BEGIN
    PATCH_IF option=1 BEGIN
      REPLACE_TEXTUALLY ~%van_wsp%~ ~%new_wsp%~
      REPLACE_TEXTUALLY ~%gtu_wsp%~ ~%new_wsp%~
    END
    PATCH_IF option=2 || option=3 BEGIN
      REPLACE_TEXTUALLY ~%van_wsp%~ ~%new_spl%~
      REPLACE_TEXTUALLY ~%gtu_wsp%~ ~%new_spl%~
    END
    PATCH_IF option=4 BEGIN
      REPLACE_TEXTUALLY ~%van_wsp%~ ~~
      REPLACE_TEXTUALLY ~%gtu_wsp%~ ~~
    END
  END

END

STRING_SET 9572 ~%ft%~
STRING_SET 9573 ~%fc%~
STRING_SET 9574 ~%fm%~
STRING_SET 9575 ~%mt%~
STRING_SET 9576 ~%fmt%~
STRING_SET 9577 ~%mc%~
STRING_SET 9578 ~%tc%~
STRING_SET 9579 ~%fd%~
STRING_SET 9580 ~%cr%~
STRING_SET 9581 ~%fmc%~


